%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Caso de estudio
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\chapter{Caso de estudio}
\label{ch:4}

En el capítulo anterior se describió el diseño e implementación de records extensibles en Idris realizado en este trabajo. Sin embargo, el único uso de tales records visto hasta ahora fue en algunos ejemplos básicos. En este capítulo se muestra un caso de estudio más elaborado en el cual se utilizan los records extensibles desarrollados en este trabajo para poder solucionarlo.

\section{Descripción del caso de estudio}

El caso de estudio consiste en un pequeño lenguaje de expresiones aritméticas con variables y constantes. Se define este lenguaje en Idris como un DSL (\textit{Domain Specific Language}), lo cual permite crear términos de este lenguaje, y luego evaluarlos en Idris.

Este lenguaje está compuesto por:

\begin{itemize}
\item Literales dados por números naturales.
\item Variables.
\item Sumas de expresiones.
\item Expresiones let.
\end{itemize}

Este lenguaje permite definir expresiones aritméticas formadas por valores numéricos, variables y sumas de expresiones. Los siguientes son ejemplos de expresiones de este lenguaje:

\begin{code}
x
x + 3
let x = 3 in x + 3
let y = 10 in (let x = 3 in x + 3)
\end{code}

La gramática del lenguaje en BNF se muestra en la Figura \ref{fig:RulesBNF}.

\begin{figure}[h]

\centering
Exp ::= n | x | e1 + e2 | let x = n in e\\

Donde n $\in$ Nat; x $\in$ Var; e, e1 y e2 $\in$ Exp
\caption{Gramática BNF}
\label{fig:RulesBNF}
\end{figure}



Records extensibles son utilizados para poder verificar que las expresiones están bien formadas, y que las llamadas al evaluador son válidas, todo en tiempo de compilación.

Dada una expresión, como \texttt{x + 3}, para evaluarla es necesario tener un ambiente con valores para sus variables libres. En el caso de \texttt{x + 3} es necesario tener un ambiente con el valor de la variable \texttt{x}, de lo contrario la evaluación de esa expresión no es válida. Con Idris, es posible verificar en tiempo de compilación que el ambiente utilizado para evaluar una expresión contiene valores para las variables libres de ésta. Al llamar al evaluador se verifica que los tipos sean correctos.

Los records extensibles son utilizados para definir tal ambiente al momento de evaluar una expresión. Como el ambiente contiene una lista de variables con sus valores, éstos pueden ser representados como un record, donde cada etiqueta es una variable libre. El ambiente puede ser extendido con nuevas variables, o se le pueden eliminar variables fuera de alcance. Al ser un record extensible se tiene la garantía de que ninguna variable se encuentra repetida en el ambiente, y por otra parte se tiene una prueba de que toda variable libre pertenece al ambiente.

Las propiedades descritas anteriormente son posibles utilizando records extensibles y tipos dependientes. Sin estas herramientas, es posible llamar al evaluador con un ambiente sin las variables libres necesarias para evaluar la expresión, fallando en tiempo de ejecución. Sin records extensibles tampoco se tiene una estructura de datos adecuada (con las propiedades deseadas) para almacenar las variables en el ambiente.

\section{Definición de una expresión}

Toda expresión tiene un conjunto de variables libres, que se definen con las reglas de la Figura \ref{fig:RulesFreeVariables}.

\begin{figure}[h]

\centering

\begin{subfigure}{0.5\linewidth}
\centering
\inference[lit]{n : Nat}{n \leadsto \emptyset}
\end{subfigure}%

\bigskip

\begin{subfigure}{0.5\linewidth}
\centering
\inference[var]{v : Var}{v \leadsto \{v\}}
\end{subfigure}%

\bigskip

\begin{subfigure}{0.5\linewidth}
\centering
\inference[add]{e1 \leadsto \Delta1 & e2 \leadsto \Delta2}{e1 + e2 \leadsto \Delta1 \cup \Delta2 }
\end{subfigure}%

\bigskip

\begin{subfigure}{0.5\linewidth}
\centering
\inference[let]{e \leadsto \Delta & n : Nat & v : String}{\textbf{let} \, v = n \, \textbf{in} \, e \leadsto \Delta \setminus \{v\}}
\end{subfigure}%

\caption{Reglas de variables libres}
\label{fig:RulesFreeVariables}
\end{figure}

Estas reglas pueden implementarse en Idris en un único tipo \texttt{Exp}.

Una expresión \texttt{e : Exp fv} indica que \texttt{e} es una expresión y \texttt{fv} es su conjunto de variables libres. Como ejemplo, \texttt{e : Exp [''x'', ''y'']} contiene a \texttt{''x''} y \texttt{''y''} como variables libres. Se decidió incluir la información de variables libres en el tipo para poder facilitar la implementación del evaluador.

\begin{code}
data VarDec : String -> Type where
  (:=) : (var : String) -> Nat -> VarDec var
\end{code}

Un valor de tipo \texttt{VarDec x} contiene la variable \texttt{x} y un natural. Representa la declaración de una variable, como \texttt{''x'' := 10 : VarDec ''x''}.

\begin{code}
data Exp : List String -> Type where
  Add : Exp fvs1 -> Exp fvs2 ->
    IsLeftUnion_List fvs1 fvs2 fvsRes ->
    Exp fvsRes
  Var : (l : String) -> Exp [l]
  Lit : Nat -> Exp []
  Let : VarDec var -> Exp fvsInner ->
    DeleteLabelAtPred_List var fvsInner fvsOuter ->
    Exp fvsOuter
\end{code}

Cada constructor representa cada regla de formación de una expresión. A su vez, cada constructor sigue las reglas de construcción de variables libres definidas anteriormente.

En el caso de una expresión dada por una variable la expresión contiene una única variable libre. Si es un literal entonces no hay variables libres.

En el caso de la suma de dos expresiones el conjunto de variables libres es la unión por izquierda de las listas de variables libres de los sumandos. Esto se realiza utilizando el predicado \texttt{IsLeftUnion} visto en el capítulo anterior, solo que éste funciona sobre listas  \texttt{List lty} en vez de listas de campos con etiquetas \texttt{LabelList lty}.

\begin{code}
data IsLeftUnion_List : List lty -> List lty ->
  List lty -> Type where
  IsLeftUnionAppend_List :
    {ls1, ls2, ls3 : List lty} ->
    DeleteLabelsPred_List ls1 ls2 ls3 ->
    IsLeftUnion_List ls1 ls2 (ls1 ++ ls3)

data DeleteLabelsPred_List : List lty -> List lty ->
  List lty -> Type where
  EmptyLabelList_List : DeleteLabelsPred_List {lty} [] ls ls
  DeleteFirstOfLabelList_List : 
    DeleteLabelAtPred_LIst l lsAux lsRes ->
    DeleteLabelsPred_List ls1 ls2 lsAux ->
    DeleteLabelsPred_List {lty} (l :: ls1) ls2 lsRes

data DeleteLabelAtPred_List : lty -> List lty ->
  List lty -> Type where
  EmptyRecord_List : {l : lty} -> DeleteLabelAtPred_List l [] []
  IsElem_List : {l : lty} -> DeleteLabelAtPred_List l (l :: ls) ls
  IsNotElem_List : {l1 : lty} -> Not (l1 = l2) ->
    DeleteLabelAtPred_List l1 ls1 ls2 ->
    DeleteLabelAtPred_List l1 (l2 :: ls1) (l2 :: ls2)
\end{code}

Un ejemplo sería el siguiente:

\begin{code}
IsLeftUnion_List ["A", "B"] ["B", "C"]
  ["A", "B", "C"]
\end{code}

En el caso de un let las variables libres corresponden a las variables libres de la expresión del let menos la variable local. Esto se realiza con el predicado \texttt{DeleteLabelAtPred\_List}, que se comporta igual que \texttt{DeleteLabelAtPred} definido en el capítulo anterior, solo que se aplica sobre listas \texttt{List lty} en vez de listas de campos con etiquetas \texttt{LabelList lty}. Su definición se mostró más arriba.

Un ejemplo de tal predicado sería el siguiente:

\begin{code}
DeleteLabelAtPred_List "A" ["A", "B", "C"]
  ["B", "C"]
\end{code}

Al igual que en el capítulo anterior, se decidió utilizar predicados que representan las computaciones de unión por izquierda y eliminación de una etiqueta porque simplifican el desarrollo, permitiendo realizar pattern matching de forma más sencilla.

\subsection{Construcción de una expresión}

Para poder construir expresiones de este lenguaje, se definieron funciones auxiliares (habitualmente denominados ''smart constructors'') que construyen valores del tipo \texttt{Exp}.

\begin{code}
var : (l : String) -> Exp [l]
var l = Var l

lit : Nat -> Exp []
lit n = Lit n

add : Exp fvs1 -> Exp fvs2 -> Exp (leftUnion fvs1 fvs2)
add {fvs1} {fvs2} e1 e2 = Add e1 e2
  (fromLeftUnionFuncToPred {ls1=fvs1} {ls2=fvs2})

eLet : VarDec var -> Exp fvs -> Exp (deleteAtList var fvs)
eLet {var} {fvs} varDec e = Let varDec e
  (fromDeleteLabelAtListFuncToPred {l=var} {ls=fvs})
\end{code}

Las cuatro funciones simplemente aplican el constructor correspondiente. Las funciones \texttt{add} y \texttt{eLet} tienen la particularidad de que realizan las computaciones sobre las listas de variables, y construyen los predicados correspondientes dadas esas computaciones. Las funciones \texttt{leftUnion} y \texttt{deleteAtList} computan valores análogos a \texttt{IsLeftUnion\_List} y \texttt{DeleteAtLabel\_List}, cumpliendo las siguientes propiedades:

\begin{code}
DeleteLabelAtPred_List l ls1 ls2 <-> ls2 = deleteAtList l ls1

IsLeftUnion_List ls1 ls2 ls3 <-> ls3 = leftUnion ls1 ls2
\end{code}

Las funciones \texttt{fromLeftUnionFuncToPred} y \texttt{fromDeleteLabelAtListFuncToPred} son las que representan las propiedades anteriores:

\begin{code}
fromDeleteLabelAtListFuncToPred : DecEq lty => {l : lty} ->
  {ls : List lty} -> DeleteLabelAtPred_List l ls (deleteAtList l ls)

fromLeftUnionFuncToPred : DecEq lty => {ls1, ls2 : List lty} ->
  IsLeftUnion_List {lty} ls1 ls2 (leftUnion ls1 ls2)
\end{code}

A continuación se muestran ejemplos de expresiones construidas usando los ''smart constructors'':

\begin{code}
exp1 : Exp ["x", "y"]
exp1 = add (var "x") (add (lit 1) (var "y"))

exp2 : Exp ["y"]
exp2 = eLet ("x" := 10) $ add (var "x") (var "y")

exp3 : Exp []
exp3 = eLet ("y" := 5) exp4
\end{code}

También se decidió incluir otra función útil para construir expresiones. Se trata de \textit{local}, que permite definir un binding local de variables. Su uso sería el siguiente:

\begin{code}
exp1 : Exp []
exp1 = local ["x" := 10] $ lit 1

exp2 : Exp []
exp2 = local ["x" := 10, "y" := 9] $ add (var "x") (var "y")
\end{code}

A diferencia de let, \texttt{local} permite declarar varias variables simultáneamente. Su implementación es la siguiente:

\begin{code}
data LocalVariables : List String -> Type where
  Nil : LocalVariables []
  (::) : VarDec l -> LocalVariables ls ->
    LocalVariables (l :: ls)

localPred : (vars : LocalVariables localVars) ->
  (innerExp : Exp fvsInner) -> {isSet : IsSet localVars} ->
  Exp (deleteList localVars fvsInner)

local : (vars : LocalVariables localVars) -> (innerExp : Exp fvsInner) ->
  TypeOrUnit (isSet localVars) (Exp (deleteList localVars fvsInner))
local {localVars} {fvsInner} vars innerExp =
  mkTypeOrUnit (isSet localVars)
    (\localIsSet => localPred vars innerExp {isSet=localIsSet})
\end{code}

La función \texttt{local} utiliza la misma técnica que utilizamos para \texttt{consRecAuto} y otras funciones sobre records con \texttt{TypeOrUnit}. En este caso, \texttt{local} permite construir una prueba de \texttt{IsSet localVars} automáticamente en tiempo de compilación. Esta prueba evita que se defina la misma variable varias veces, como \texttt{local [''x'' := 1, ''x'' := 2]}.

Tal declaración de variables se define en \texttt{LocalVariables}, que es un tipo que representa la lista de declaraciones locales de variables, y en su tipo mantiene la lista de variables declaradas. Por ejemplo, se tiene \texttt{[''x'' := 10, ''y'' := 4] : LocalVariables [''x'', ''y'']}.

La expresión resultante calcula sus variables libres con \texttt{deleteList}. Esta función es la análoga a \texttt{DeleteLabelsPred\_List} que cumple esta propiedad:

\begin{code}
DeleteLabelsPred_List ls1 ls2 ls3 <-> ls3 = deleteList ls1 ls2
\end{code}

La implementación de \texttt{localPred} se puede ver en el apéndice, pero básicamente realiza una construcción secuencial de \texttt{Let}s para cada variable declarada en el binding. Básicamente, las siguientes expresiones son equivalentes:

\begin{code}
local ["x" := 10] $ lit 1 <-> eLet ("x" := 10) $ lit 1

local ["x" := 10, "y" := 9] $ add (var "x") (var "y") <->
  eLet ("x" := 10) $ eLet ("y" := 9) $ add (var "x") (var "y)
\end{code}

Como \texttt{local} verifica que las variables definidas no se repitan, si se llama con variables repetidas se muestra un error de compilación:

\begin{code}
e : Exp []
e = local ["x" := 10, "x" := 11] $ var "x"

  When checking right hand side of e with expected type
    Exp []
  
  Type mismatch between
    TypeOrUnit (isSet ["x", "x"])
      (Exp (deleteList ["x", "x"] ["x"]))
      (type of 
        local ["x" := 10, "x" := 11 (var "x"))

  and
    Exp [] (Expected type)

  Specifically:
    Type mismatch between
      TypeOrUnit (isSet ["x", "x"]) 
        (Exp (deleteList ["x", "x"] ["x"]))
    and
      Exp []
\end{code}

\section{Evaluación de una expresión}

La evaluación de una expresión se realiza con esta función:

\begin{code}
interpEnv : Ambiente fvsEnv -> IsSubSet fvs fvsEnv ->
  Exp fvs -> Nat
\end{code}

Dada una expresión \texttt{Exp fvs}, con una lista de variables libres \texttt{fvs}, se necesita tener un ambiente \texttt{Ambiente fvsEnv} con variables \texttt{fvsEnv}. El ambiente debe tener definidos valores para todas las variables libres de la expresión.

Por ejemplo, si se tiene \texttt{Exp [''x'', ''y'']}, entonces deben tener ambientes que incluyan esas variables, como \texttt{Ambiente [''x'', ''y'']}, o \texttt{Ambiente [''x'', ''y'', ''z'']}, o \texttt{Ambiente [''x'', ''w'', ''y'', ''z'']}. El ambiente puede tener variables extras, pero siempre debe tener valores para las variables libres de la expresión. Esto garantiza que cualquier llamada a \texttt{interpEnv} sea válida, ya que toda expresión puede evaluarse si se tienen valores para sus variables libres.

El tipo \texttt{IsSubSet fvs fvsEnv} refleja esa relación, como se puede ver en estos ejemplos:

\begin{code}
IsSubSet ["x"] ["x"]
IsSubSet ["x"] ["x", "y"]
IsSubSet ["x", "y"] ["x", "y", "z"]
\end{code}

Su definición es la siguiente:

\begin{code}
data IsSubSet : List lty -> List lty -> Type where
  IsSubSetNil : IsSubSet [] ls
  IsSubSetCons : IsSubSet ls1 ls2 -> Elem l ls2 ->
    IsSubSet (l :: ls1) ls2
\end{code}

En el caso base la lista vacía es subconjunto de cualquier posible lista. En el caso inductivo, si un elemento a agregar pertenece a la lista, entonces agregando ese elemento a un subconjunto de esa lista también es un subconjunto.

Por último se define el ambiente de esta forma:

\begin{code}
data Ambiente : List String -> Type where
  MkAmbiente : Record {lty=String} (AllNats ls) -> Ambiente ls

AllNats : List lty -> LabelList lty
AllNats [] = []
AllNats (x :: xs) = (x, Nat) :: AllNats xs
\end{code}

\texttt{AllNats} es una función que toma una lista de etiquetas y les asigna el tipo \texttt{Nat} a todas. Por ejemplo:

\begin{code}
AllNats ["x", "y"] = [("x", Nat), ("y", Nats)]
\end{code}

Esto permite poder utilizar el record \texttt{Record \{lty=String\} (AllNats ls)}. El ambiente se trata de un record extensible, donde sus etiquetas son strings y sus campos siempre son del tipo \texttt{Nat}.

Un ejemplo (con una pseudo-sintaxis) sería:

\begin{code}
{ "x = 10, "y" = 20, "z" = 22 } :
  Record [("x", Nat), ("y", Nat), ("z", Nat)]
\end{code}

La evaluación de una expresión cerrada (o sea, sin variables libres) se hace de la siguiente manera:

\begin{code}
interp : Exp [] -> Nat
interp = interpEnv (MkAmbiente {ls=[]} emptyRec) IsSubSetNil
\end{code}

Ejemplos de tal evaluación serían los siguientes:

\begin{code}
interp $ local ["x" := 10, "y" := 9] $ add (var "x") (var "y")
-- 19

interp $ eLet ("x" := 10) $ add (var "x") (lit 2)
-- 12

interp $ add (lit 1) (lit 2)
-- 3
\end{code}

Como la función \texttt{interp} solo se puede llamar con expresiones cerradas, si se llama con una expresión abierta se muestra un error de compilación:

\begin{code}
v : Nat
v = interp $ var "x"

  When checking right hand side of v with expected type 
    Nat

  When checking an application of function CasoDeEstudio.interp:
    Type mismatch between
      Exp [l] (Type of var l)
    and
      Exp [] (Expected type)
    
    Specifically:
      Type mismatch between
        [l]
      and
        []
\end{code}

La implementación completa del evaluador se muestra a continuación.

\subsection{Expresiones de literales}

El caso de un literal es el más sencillo:

\begin{code}
interpEnv env subSet (Lit c) = c
\end{code}

Significa retornar tal valor como un resultado.

\subsection{Expresiones con una variable}

Una expresión que declara una variable necesita tener esa variable en el ambiente y obtener el valor de ella:

\begin{code}
interpEnv {fvsEnv} (MkAmbiente rec) subSet (Var l) =
  let hasField = HasFieldHere {l} {ty=Nat} {ts=[]}
    hasFieldInEnv = ifIsSubSetThenHasFieldInIt subSet hasField
  in hLookupByLabel l rec hasFieldInEnv
\end{code}

Como el ambiente es un record \texttt{Record (AllNats fvsEnv)}, es necesario obtener la prueba de que \texttt{l} pertenece a ese record, y luego hacer un lookup.

Primero, se obtiene una prueba de \texttt{HasField l [(l, Nat)] Nat} realizando esta llamada:

\begin{code}
hasField = HasFieldHere {l} {ty=Nat} {ts=[]}
\end{code}

Luego se hace uso de la siguiente función para obtener la prueba de que \texttt{l} pertenece al ambiente:

\begin{code}
ifIsSubSetThenHasFieldInIt : DecEq lty => {ls1, ls2 : List lty} ->
  IsSubSet ls1 ls2 -> HasField l (AllNats ls1) Nat ->
  HasField l (AllNats ls2) Nat
\end{code}

Esta función indica que si un elemento pertenece a una lista \texttt{ls1} y tiene tipo \texttt{Nat}, y esa lista \texttt{ls1} es un subconjunto de otra lista \texttt{ls2}, entonces ese elemento también pertenece a la lista \texttt{ls2} con tipo \texttt{Nat}. Básicamente es un teorema que prueba que la propiedad de pertenencia a una lista (de tipos \texttt{Nat}) se preserva.

Esta función se utiliza en la siguiente llamada:

\begin{code}
hasFieldInEnv = ifIsSubSetThenHasFieldInIt subSet hasField
\end{code}

\texttt{hasFieldInEnv} tiene tipo \texttt{HasField l (Allnats fvsEnv) Nat}. Como el record del ambiente tiene tipo \texttt{Record (AllNats fvsEnv)}, entonces se tiene una prueba de que el elemento pertenece al ambiente, por lo que se puede retornar su valor con la siguiente invocación:

\begin{code}
hLookupByLabel l rec hasFieldInEnv
\end{code}

En este caso la llamada a lookup es válida en tiempo de compilación. Por cómo se definió el evaluador, toda variable libre de la expresión pertenece al ambiente, por lo cual es posible probar que una variable específica de \texttt{Var l} pertenece al ambiente, y por lo tanto es posible obtener su valor. En ningún momento el lookup va a fallar en tiempo de ejecución porque no pudo encontrar la variable.

\subsection{Expresiones con suma}

Una expresión del tipo suma es sencilla de evaluar. Se necesitan evaluar las subexpresiones de ella, y luego sumar sus resultados:

\begin{code}
interpEnv env subSet (Add e1 e2 isUnionFvs) =
  let (subSet1, subSet2) =
      ifIsSubSetThenLeftUnionIsSubSet subSet isUnionFvs
    res1 = interpEnv env subSet1 e1
    res2 = interpEnv env subSet2 e2
  in res1 + res2
\end{code}

El evaluador tiene tipo \texttt{Ambiente fvsEnv -> IsSubSet fvs fvsEnv -> Exp fvs -> Nat}. Cada subexpresión tiene tipo \texttt{Exp fvs1} y \texttt{Exp fvs2} respectivamente. Para poder evaluar tales subexpresiones, como ya se tiene un ambiente de tipo \texttt{Ambiente fvsEnv}, es necesario tener una prueba de \texttt{IsSubSet fvs1 fvsEnv} y \texttt{IsSubSet fvs2 fvsEnv} para poder llamar al evaluador.

Eso es lo que efectivamente realiza esta función:

\begin{code}
ifIsSubSetThenLeftUnionIsSubSet : DecEq lty =>
  {ls1, ls2, lsSub1, lsSub2 : List lty} -> IsSubSet ls1 ls2 ->
  IsLeftUnion_List lsSub1 lsSub2 ls1 ->
  (IsSubSet lsSub1 ls2, IsSubSet lsSub2 ls2)
\end{code}

Esta función toma una prueba de que \texttt{ls1} es subconjunto de \texttt{ls2}, una prueba de que \texttt{ls1} es el resultado de la unión por izquierda de \texttt{lsSub1} y \texttt{lsSub2}, y retorna las pruebas de que \texttt{lsSub1} y \texttt{lsSub2} son subconjuntos de \texttt{ls2}. Prueba que la unión por izquierda preserva el predicado de ser subconjunto de una lista para ambos componentes de la unión.

En el evaluador, se realiza la llamada de la siguiente forma:

\begin{code}
(subSet1, subSet2) =
  ifIsSubSetThenLeftUnionIsSubSet subSet isUnionFvs
\end{code}

Donde \texttt{isUnionFvs} tiene tipo \texttt{IsLeftUnion\_List fvs1 fvs2 fvs} y \texttt{subSet} tipo \texttt{IsSubSet fvs fvsEnv}. Por lo tanto, esta llamada obtiene las pruebas de \texttt{IsSubSet fvs1 fvsEnv} y \texttt{IsSubSet fvs2 fvsEnv} deseadas.

Con tales pruebas se pueden evaluar las subexpresiones de esta forma:

\begin{code}
res1 = interpEnv env subSet1 e1
res2 = interpEnv env subSet2 e2
\end{code}

Al tener ya los resultados de tales evaluaciones, el resultado final es su simple suma:

\begin{code}
res1 + res2
\end{code}

\subsection{Expresiones con sustitución de variable}

Una expresión let es más difícil de evaluar que los demás casos, ya que requiere la manipulación del ambiente de variables. La implementación completa de este caso se puede ver en la Figura \ref{fig:InterpImplLet}.

\begin{figure}[h]

\begin{code}
interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
    with (isElem var fvsEnv)
  interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
    | Yes varInEnv =
    let (MkAmbiente recEnv) = env
      hasField = ifIsElemThenHasFieldNat varInEnv 
      newRec = hUpdateAtLabel var n recEvn hasField
      newEnv = MkAmbiente newRec
      consSubSet =
        ifIsSubSetThenSoIfYouDeleteLabel delAt subSet {l=var}
      newSubSet = ifConsIsElemThenIsSubSet consSubSet varInEnv
    in interpEnv newEnv newSubSet e
 
  interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
    | No notVarInEnv =
    let (MkAmbiente recEnv) = env
      newRec = consRec var n recEnv
        {notElem=ifNotElemThenNotInNats notVarInEnv}
      newEnv = MkAmbiente newRec {ls=(var :: fvsEnv)}
      newSubSet =
        ifIsSubSetThenSoIfYouDeleteLabel delAt subSet {l=var}
    in interpEnv newEnv newSubSet e
\end{code}

\caption{Evaluación de una sustitución de variable}
\label{fig:InterpImplLet}
\end{figure}

El primer paso a tomar es saber si la variable \texttt{var} se encuentra en el ambiente o no. Es posible que la variable \texttt{var} ya tenga un valor \texttt{n2} asignado en el ambiente, pero si es así, tal valor debe ser sustituido por \texttt{n} cuando se intente evaluar la subexpresión \texttt{e}. Si la variable no se encuentra en el ambiente, entonces debe ser agregado al mismo al momento de evaluar la subexpresión.

Veremos cómo se realiza esto en la implementación misma:

\begin{code}
interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
    with (isElem var fvsEnv)
\end{code}

El primer paso entonces consiste en tomar las variables del ambiente \texttt{fvs} y verificar si \texttt{var} pertenece a ellas con la llamada a \texttt{isElem var fvsEnv}. Esto trae dos casos posibles, uno donde pertenece y otro donde no.

Primero abarcaremos el caso donde no existe:

\begin{code}
interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
  | No notVarInEnv =
  let (MkAmbiente recEnv) = env
\end{code}

Se obtiene la prueba \texttt{notVarInEnv : Not (Elem var fvsEnv)}, y luego se extrae el record de tipo \texttt{Record \{lty=String\} (AllNats fvsEnv)} del ambiente.

\begin{code}
newRec = consRec var n recEnv
  {notElem=ifNotElemThenNotInNats notVarInEnv}
\end{code}

Como la variable no pertenece al ambiente, entonces se agrega su etiqueta \texttt{var} y su valor \texttt{n} al record, extendiendolo con \texttt{consRec}. Recordemos el tipo de \texttt{consRec}:

\begin{code}
consRec : DecEq lty => {ts : LabelList lty} -> {t : Type} ->
  (l : lty) -> (val : t) -> Record ts ->
  {notElem : Not (ElemLabel l ts)} -> Record ((l, t) :: ts)
\end{code}

La función \texttt{consRec} utilizada aquí no hace uso de \texttt{IsLabelSet} sino que utiliza \texttt{Not (Elemlabel l ts)}.

Para poder extender el record se necesita una prueba de \texttt{Not (ElemLabel l ts)}, es decir, que la etiqueta a agregar no exista en el record. Como este caso surge de tener una prueba de que la variable está en el ambiente, es posible construir la prueba con la siguiente función:

\begin{code}
ifNotElemThenNotInNats : Not (Elem x xs) ->
  Not (ElemLabel x (AllNats xs))
\end{code}

Recordemos que se tiene la prueba \texttt{notVarInEnv : Not (Elem var fvsEnv)}, pero para poder extender el record se necesita una prueba de \texttt{Not (ElemLabel var (AllNats fvsEnv))}. Esta función auxiliar realiza esa transformación, conociendo que si un elemento no pertenece a una lista como \texttt{[''x'', ''y'']}, entonces tampoco va a pertenecer a una donde se agrega el tipo \texttt{Nat}, como \texttt{[(''x'', Nat), (''y'', Nat)]}.

Luego de extender el record, se obtiene el nuevo ambiente de forma simple:

\begin{code}
newEnv = MkAmbiente newRec {ls=(var :: fvsEnv)}
\end{code}

Ahora que se tiene el nuevo ambiente, para poder evaluar la subexpresión en él, es necesario tener una prueba de que las variables libres de la subexpresión son un subconjunto de las del nuevo ambiente. Esto se realiza de esta forma:

\begin{code}
newSubSet =
  ifIsSubSetThenSoIfYouDeleteLabel delAt subSet {l=var}
\end{code}

Conociendo que \texttt{subSet} es la prueba de \texttt{IsSubSet fvs fvsEnv}, y \texttt{delAt} de \texttt{DeleteLabelAtPred\_List var fvsInner fvs}, entonces se necesita poder obtener la prueba de \texttt{IsSubSet fvsInner (var :: fvsEnv)}. Básicamente, si se agrega \texttt{var} a ambas listas la propiedad de ser un subconjunto se preserva.

Esta prueba se obtiene con la siguiente función:

\begin{code}
ifIsSubSetThenSoIfYouDeleteLabel :
  DeleteLabelAtPred_List l ls1 ls3 ->
  IsSubSet ls3 ls2 -> IsSubSet ls1 (l :: ls2)
\end{code}

Ahora se tiene el nuevo ambiente \texttt{Ambiente (var :: fvsEnv)}, se tiene la prueba de \texttt{IsSubSet fvsInner (var :: fvsEnv)} y la subexpresión \texttt{Exp fvsInner}. Con estos términos se puede evaluar tal subexpresión de esta forma, terminando la evaluación de la expresión en su conjunto:

\begin{code}
interpEnv newEnv newSubSet e
\end{code}

Ahora solo queda realizar la evaluación cuando la variable \texttt{var} sí pertenece al ambiente, en este caso:

\begin{code}
interpEnv {fvsEnv} env subSet (Let (var := n) e delAt)
  | Yes varInEnv =
  let (MkAmbiente recEnv) = env
\end{code}

Se tiene la prueba \texttt{varInEnv : Elem var fvsEnv}. Luego se extrae el record de tipo \texttt{Record \{lty=String\} (AllNats fvsEnv)} del ambiente.

Como la variable pertenece al ambiente, entonces es necesario reemplazar su valor con \texttt{n}. Para ello, primero se debe obtener la prueba de que tal variable pertenece al record con tipo \texttt{Nat}:

\begin{code}
hasField = ifIsElemThenHasFieldNat varInEnv
\end{code}

Para esto se utiliza la siguiente función auxiliar:

\begin{code}
ifIsElemThenHasFieldNat : Elem l ls -> HasField l (AllNats ls) Nat
\end{code}

Esta función simplemente transforma una prueba a otra. \texttt{Elem l ls} es equivalente a \texttt{HasField l (AllNats ls) Nat}, porque sabemos que \texttt{AllNats} no agrega información a la lista más que todos tienen el tipo \texttt{Nat}.

Para actualizar el record, se utiliza la siguiente función:

\begin{code}
hUpdateAtLabel : DecEq lty => (l : lty) ->
  ty -> Record ts -> HasField l ts ty -> Record ts
\end{code}

Esta función se vió en los ejemplos del capítulo anterior. Toma un record, una etiqueta de tal, una prueba de que esa etiqueta existe en el recordd y tiene un tipo \texttt{ty}, y actualiza el record con un valor del tipo \texttt{ty}. En este caso, se tiene una prueba de \texttt{HasField l (AllNats ls) Nat}, por lo que se puede actualizar el record pasando un nuevo valor de tipo \texttt{Nat} de la siguiente forma:

\begin{code}
newRec = hUpdateAtLabel var n recEnv hasField
newEnv = MkAmbiente newRec
\end{code}

Al tener el record se puede crear el nuevo ambiente de tipo \texttt{Ambiente fvsEnv}, idéntico al anterior, solo con el valor \texttt{n} para la etiqueta \texttt{var}.

Ahora, al igual que el caso donde \texttt{var} no pertenecía al ambiente, es necesario construir la prueba de que la lista de variables libres de la subexpresión es un subconjunto de las del ambiente. Al igual que el caso anterior, se utilizará la misma función:

\begin{code}
consSubSet =
  ifIsSubSetThenSoIfYouDeleteLabel delAt subSet {l=var}
\end{code}

Como en el caso anterior, esta llamada retorna una prueba de \texttt{IsSubSet fvsInner (var :: fvsEnv)}. Sin embargo, en este caso el ambiente no es de tipo \texttt{Ambiente (var :: fvsEnv)}, sino que su tipo nunca cambió y sigue siendo \texttt{Ambiente fvsEnv}. Por eso es necesaria una prueba de \texttt{IsSubSet fvsInner fvsEnv}.

\begin{code}
newSubSet = ifConsIsElemThenIsSubSet consSubSet varInEvn
\end{code}

Como se muestra más arriba, tal prueba se consigue con la siguiente función:

\begin{code}
ifConsIsElemThenIsSubSet : IsSubSet ls1 (l :: ls2) ->
  Elem l ls2 -> IsSubSet ls1 ls2
\end{code}

Esta función indica que si se tiene una prueba de que una lista \texttt{ls1} es subconjunto de \texttt{l :: ls2}, pero \texttt{l} ya pertenece a \texttt{ls2}, entonces es posible eliminarla y esto no altera la propiedad de ser subconjunto.

En el caso actual, al tener \texttt{IsSubSet fvsInner (var :: fvsEnv)} y \texttt{Elem var fvsEnv}, se sabe que \texttt{fvsInner} es subconjunto de \texttt{fvsEnv}, representado por la prueba de \texttt{IsSubSet fvsInner fvsEnv}.

Ahora se tiene el nuevo ambiente \texttt{Ambiente fvsEnv}, se tiene la prueba de \texttt{IsSubSet fvsInner fvsEnv} y la subexpresión \texttt{Exp fvsInner}. Con estos términos se puede evaluar la subexpresión de esta forma, terminando la evaluación de la expresión en su conjunto:

\begin{code}
interpEnv newEnv newSubSet e
\end{code}

Como se pudo evaluar la subexpresión para los dos casos (si \texttt{var} pertenece al ambiente o no), ya termina la evaluación de la expresión \texttt{let var := n in e}.
